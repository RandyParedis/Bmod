grammar org.xtext.bmod.Bmod with org.eclipse.xtext.common.Terminals

generate bmod "http://www.xtext.org/bmod/Bmod"
import "http://www.xtext.org/bmod/BReact"

Model:
	Floorplan
;

Floorplan:
	{Floorplan}
	(imports += Import)*
	(
		rooms += Room | doors += Door |
		persons += Person | exits += Exit | fires += Fire |
		signs += EmergencySign
	)*
;

Import:
	'import' importURI=STRING
;

Room:
	'Room' name=VARNAME (hasCapacity?='[' capacity=INT ']')?
		areas += Area ('and' areas += Area)*
;

Door:
	'Door' name=VARNAME 'from' from=Coordinate 'to' to=Coordinate
;

Person:
	'Person' name=VARNAME 'in' location=Coordinate ':' action=[ActionDesc|VARNAME]
;

Exit:
	'Exit' 'in' location=Coordinate
;

Fire:
	'Fire' 'in' location=Coordinate
;

EmergencySign:
	'EmergencySign' 'from' from=[Door|VARNAME] ref=DoorRef
;

DoorRef:
	'to' to=[Door|VARNAME]
;
	
Coordinate: '(' x=INT ',' y=INT ')';

Area:
	'from' from=Coordinate 'to' to=Coordinate
	('without' without+=Coordinate (',' without+=Coordinate)*)?
;

terminal VARNAME:
	('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
;
