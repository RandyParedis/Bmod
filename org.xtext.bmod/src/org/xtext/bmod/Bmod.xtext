grammar org.xtext.bmod.Bmod with org.eclipse.xtext.xbase.Xbase
//grammar org.xtext.bmod.Bmod with org.eclipse.xtext.common.Terminals

generate bmod "http://www.xtext.org/bmod/Bmod"

Model:
	Floorplan
;

Floorplan:
	{Floorplan}
	(
		perceptions += PerceptionLevel | actions += ActionProfile |
		rooms += Room | doors += Door |
		persons += Person | exits += Exit | fires += Fire |
		signs += EmergencySign | dancons += DangerousCondition
	)*
;

Room:
	'Room' name=VARNAME ('named' named=STRING)?
		areas += Area ('and' areas += Area)*
;

Door:
	'Door' name=VARNAME 'from' from=Coordinate 'to' to=Coordinate
;

PerceptionLevel:
	'PerceptionLevel' name=VARNAME '(' person=VARNAME ',' objects=VARNAME ')'
		body=XBlockExpression
;

ActionProfile:
	'ActionProfile' name=VARNAME '(' person=VARNAME ',' objects=VARNAME ')'
		body=XBlockExpression
;

Perception:
	(existing=PerceptionEnum)
	|(custom=[PerceptionLevel|VARNAME])
;

Action:
	(existing=ActionEnum)
	|(custom=[ActionProfile|VARNAME])
;

enum PerceptionEnum:
	attentive = 'attentive'   | relaxed = 'relaxed' |
	optimistic = 'optimistic' | sceptic = 'sceptic'
;

enum ActionEnum:
	newcomer = 'newcomer' | experienced = 'experienced' | panic = 'panic'
;

Person:
	'Person' name=VARNAME 'named' named=STRING 'in' location=Coordinate ':'
	perception=Perception ',' action=Action
;

Exit:
	'Exit' 'in' location=Coordinate
;

Fire:
	'Fire' 'in' location=Coordinate
;

EmergencySign:
	'Sign' 'on' on=[Door|VARNAME] to=DoorRef
;

DoorRef:
	'to' ref=[Door|VARNAME]
;

DangerousCondition:
	'Condition' 'on' on=[Room|VARNAME] amount=INT
;
	
Coordinate: '(' x=INT ',' y=INT ')';

Area:
	'from' from=Coordinate 'to' to=Coordinate
	('without' without+=Coordinate (',' without+=Coordinate)*)?
;

terminal VARNAME:
	('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
;