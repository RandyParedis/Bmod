grammar org.xtext.bmod.Bmod with org.eclipse.xtext.common.Terminals

generate bmod "http://www.xtext.org/bmod/Bmod"

Model:
	Floorplan
;

Floorplan:
	{Floorplan}
	(
		rooms += Room | doors += Door |
		persons += Person | exits += Exit |
		signs += EmergencySign | dancons += DangerousCondition
	)*
;

Room:
	'Room' name=VARNAME 'named' named=STRING
		areas += Area ('and' areas += Area)*
;

Door:
	'Door' name=VARNAME 'from' from=Coordinate 'to' to=Coordinate
;

enum Perception:
	ATTENTIVE = 'attentive'   | RELAXED = 'relaxed' |
	OPTIMISTIC = 'optimistic' | SCEPTIC = 'sceptic' 
;

enum Action:
	NEWCOMER = 'newcomer' | EXPERIENCED = 'experienced' | PANIC = 'panic'
;

Person:
	'Person' name=VARNAME 'named' named=STRING 'in' location=Coordinate ':'
	((perception=Perception ',' action=Action)|(action=Action ',' perception=Perception))
;

Exit:
	'Exit' 'in' location=Coordinate
;

EmergencySign:
	'Sign' 'on' on=[Door|VARNAME] 'to' to=[Door|VARNAME]
;

DangerousCondition:
	'Condition' 'on' on=[Room|VARNAME] amount=INT
;
	
Coordinate: '(' x=INT ',' y=INT ')';

Area:
	'from' from=Coordinate 'to' to=Coordinate
	('without' without+=Coordinate (',' without+=Coordinate)*)?
;

terminal VARNAME:
	('A'..'Z'|'a'..'z'|'_')('A'..'Z'|'a'..'z'|'0'..'9'|'_')*
;
